plugins {
    id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
    id 'com.diffplug.spotless' version '8.+'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

spotless {
    java {
        target 'src/*/java/**/*.java'

        googleJavaFormat("1.33.0")

        toggleOffOn()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

repositories {
    maven { url 'https://maven.nucleoid.xyz' }
    maven { url 'https://jitpack.io' }
    maven { url "https://api.modrinth.com/maven" }
    mavenCentral()
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation("eu.pb4:polymer-core:[${project.polymer_version}]")
    modImplementation("eu.pb4:polymer-blocks:[${project.polymer_version}]")
    modImplementation("eu.pb4:polymer-resource-pack:[${project.polymer_version}]")
    modImplementation("eu.pb4:polymer-virtual-entity:[${project.polymer_version}]")
    modImplementation("eu.pb4:polymer-autohost:${project.polymer_version}")
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName
    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
    processResources.exclude('.cache/**/*')
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
        // Automatically configured for GitHub Packages if env vars exist
        if (System.getenv("GITHUB_TOKEN")) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

// Automatically configured for Modrinth releases if env vars exist
if (System.getenv("MODRINTH_TOKEN")) {
    modrinth {
        token = System.getenv("MODRINTH_TOKEN")
        projectId = 'project_id'// This can be the project ID or the slug
        versionNumber = "${project.version}"
        versionType = "release" // This is the default -- can also be `beta` or `alpha`
        uploadFile = remapJar
        gameVersions = [((String) project.minecraft_version)]
        changelog = System.getenv("CHANGELOG")
        loaders = ["fabric"]

        dependencies {
            required.project "polymer"
        }

        syncBodyFrom = rootProject.file("README.md").text
        tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
    }
    remapJar {
        finalizedBy project.tasks.modrinth
    }
}